// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    ext {
        kotlin_version = '1.3.0'
        kotlinx_coroutines_version = '1.0.1'

        android_min_sdk = 24
        android_target_sdk = 27
        android_compile_sdk = 28

        libs = [
                rx: [
                        core: [
                                'io.reactivex.rxjava2:rxjava:2.2.0',
                                'io.reactivex.rxjava2:rxkotlin:2.3.0'
                        ],
                        android: 'io.reactivex.rxjava2:rxandroid:2.0.2'
                ],
                test: [
                        core: [
                                'junit:junit:4.12',
                                'org.assertj:assertj-core:3.8.0',
                                'org.mockito:mockito-core:2.8.47',
                                'com.nhaarman:mockito-kotlin:1.5.0',
                        ],
                        android: [
                                'org.mockito:mockito-android:2.13.0',
                                'com.android.support.test:rules:1.0.2',
                                'com.android.support.test:runner:1.0.2'
                        ],
                ],
                kotlin: [
                        core: [
                                "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version",
                                "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version",
                                "org.jetbrains.kotlinx:kotlinx-coroutines-core:$kotlinx_coroutines_version"
                        ],
                        android: "org.jetbrains.kotlinx:kotlinx-coroutines-android:$kotlinx_coroutines_version"
                ],
                inject: [
                        core: 'com.github.jbrunton:mini-container:0.2.0',
                        android: 'com.github.jbrunton:mini-container-android:0.2.0'

                ],
                android: [
                        material: [
                                'com.google.android.material:material:1.0.0-rc02',
                                'androidx.appcompat:appcompat:1.0.0-rc02',
                                'androidx.recyclerview:recyclerview:1.0.0-rc02',
                                'androidx.cardview:cardview:1.0.0-rc02',
                                'androidx.constraintlayout:constraintlayout:2.0.0-alpha2'
                        ]
                ]
        ]
    }
    repositories {
        jcenter()
        google()
        maven { url "https://oss.sonatype.org/content/repositories/snapshots" }
        maven { url "https://plugins.gradle.org/m2/" }
        maven { url "http://dl.bintray.com/kotlin/kotlin-eap" }
        flatDir {
            dirs '../aars'
        }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.2.1'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'com.google.gms:google-services:3.1.0'
        classpath "gradle.plugin.org.jlleitschuh.gradle:ktlint-gradle:3.0.1"

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

plugins {
    id "io.gitlab.arturbosch.detekt" version "1.0.0.RC6-3"
}

apply plugin: "org.jlleitschuh.gradle.ktlint"

allprojects {
    repositories {
        google()
        jcenter()
        maven { url 'https://maven.google.com' }
        maven { url "https://oss.sonatype.org/content/repositories/snapshots" }
        maven { url "http://dl.bintray.com/kotlin/kotlin-eap" }
        maven { url "https://jitpack.io" }
        flatDir {
            dirs '../aars'
        }
    }
}

detekt {
    version = "1.0.0.RC6-3"
    profile("main") {
        input = projectDir
        config = "$projectDir/detekt.yml"
        filters = ".*test.*,.*/resources/.*,.*/tmp/.*"
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

task('allUnitTests') {
    rootProject.subprojects { project ->
        def testTasks = project.getTasksByName("testDebugUnitTest", false)
        if (testTasks.isEmpty()) {
            testTasks = project.getTasksByName("test", false)
        }
        testTasks.each { dependsOn it }
    }
}

class TestMatrixPlugin implements Plugin<Project> {
    void apply(Project project) {
        def testApk = "./build/outputs/apk/androidTest/debug/${project.getName()}-debug-androidTest.apk"

        ['SmokeTest', 'RoadTest'].each { testMatrix ->
            project.task("firebase${testMatrix}", type:Exec) {
                dependsOn 'assembleDebug', 'assembleDebugAndroidTest'
                commandLine 'gcloud', 'firebase', 'test', 'android', 'run', "../firebase-test-matrices.yml:${testMatrix.toLowerCase()}",
                        '--type', 'instrumentation',
                        '--app', '../app/build/outputs/apk/debug/app-debug.apk',
                        '--test', "${testApk}"
            }
        }
    }
}

rootProject.subprojects { project ->
    def testTasks = project.getTasksByName("connectedCheck", false)
    if (!testTasks.isEmpty()) {
        project.apply(plugin: TestMatrixPlugin)
    }
}
